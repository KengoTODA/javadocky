plugins {
  id "java"
  id 'jacoco'
  id "org.jetbrains.kotlin.jvm" version "1.6.0"
  id "com.diffplug.spotless" version "6.0.4" apply false
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "net.ltgt.errorprone" version "2.0.2"
  id "org.sonarqube" version "3.3"
  id "org.springframework.boot" version "2.6.1"
  id "de.undercouch.download" version "4.1.2"
}

repositories {
  mavenCentral()
}

apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/test.gradle"
apply from: "$rootDir/gradle/integration-test.gradle"

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib"
  implementation 'org.apache.maven:maven-artifact:3.8.4'
  implementation('org.springframework.boot:spring-boot-starter-webflux'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.codehaus.janino:janino'
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

tasks.withType(JavaCompile).all {
  options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "17"
  }
}

sonarqube {
  properties {
    property "sonar.coverage.jacoco.xmlReportPaths", tasks.jacocoTestReport.reports.xml.destination
  }
}

def getBranch() {
  def process = 'git branch --show-current'.execute()
  process.waitFor()
  return process.text.trim()
}

def getHash() {
  def process = 'git rev-parse HEAD'.execute()
  process.waitFor()
  return process.text.trim()
}

bootJar {
  doFirst {
    manifest {
      attributes (
        'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Git-Branch': getBranch(),
        'Git-Hash': getHash()
      )
    }
  }
}

jar {
  enabled = false
}

task downloadNewrelic(type: Download) {
    src 'https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip'
    dest file("$buildDir")
}

task unzipNewrelic(type: Copy) {
    dependsOn downloadNewrelic
    from zipTree(file("$buildDir/newrelic-java.zip"))
    into "$buildDir"
}

tasks.build.dependsOn unzipNewrelic

defaultTasks 'spotlessApply', 'build'
