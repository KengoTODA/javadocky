plugins {
  id "java"
  id "com.diffplug.gradle.spotless" version "3.24.2"
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
  id "net.ltgt.errorprone" version "0.8.1"
  id "org.sonarqube" version "2.7.1"
  id "org.springframework.boot" version "2.1.7.RELEASE"
}

repositories {
  mavenCentral()
}

ext.sourceCompatibility = JavaVersion.VERSION_11

apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/junit.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

dependencies {
  compile('org.apache.maven:maven-artifact:3.6.1')
  compile('org.springframework.boot:spring-boot-starter-webflux'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  compileOnly('com.google.code.findbugs:jsr305:3.0.2')
  compile('org.codehaus.janino:janino:3.1.0')
  testCompile('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.jupiter'
  }
  testCompile('io.projectreactor:reactor-test')
  testCompile('com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0.pr1')

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
  integrationTestCompile "junit:junit:4.12"
  integrationTestCompile "io.github.bonigarcia:selenium-jupiter:3.3.0"
  integrationTestCompile 'com.browserstack:browserstack-local-java:0.1.0'
  integrationTestCompile 'io.github.cdimascio:java-dotenv:5.1.1'
  integrationTestCompile 'com.codeborne:selenide:5.2.8'
  integrationTestRuntime "org.junit.vintage:junit-vintage-engine"
}

task integrationTest(type: Test) {
  systemProperty 'remote', 'http://localhost:4444/wd/hub'
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

build.dependsOn integrationTest
defaultTasks 'spotlessApply', 'build'
