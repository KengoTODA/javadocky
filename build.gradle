ext.targetJavaVersion = JavaVersion.VERSION_1_8

buildscript {
  ext {
    springBootVersion = '2.0.0.M3'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "io.franzbecker:gradle-lombok:1.10"
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'io.franzbecker.gradle-lombok'

version = '0.3.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url "https://repo.spring.io/milestone"
  }
}

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

configurations {
  checkerFrameworkAnnotatedJDK {
    description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
  }
  annotationProcessors
}

dependencies {
  compile('org.apache.maven:maven-artifact:3.5.4')
  compile('org.springframework.boot:spring-boot-starter-webflux')
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('com.codeborne:selenide:4.5.1')
  testCompile('io.projectreactor:reactor-test')

  // for lombok
  def lombok = "org.projectlombok:lombok:1.16.16"
  compileOnly lombok
  annotationProcessors lombok

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime

  // for checker-framework
  ext.checkerFrameworkVersion = '2.2.0'
  ext.jdkVersion = 'jdk8'
  checkerFrameworkAnnotatedJDK "org.checkerframework:${jdkVersion}:${checkerFrameworkVersion}"
  annotationProcessors "org.checkerframework:checker:${checkerFrameworkVersion}"
  compile "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
}

def processors = [
    'org.checkerframework.checker.nullness.NullnessChecker',
    'lombok.launch.AnnotationProcessorHider\$AnnotationProcessor',
]
compileJava.options.compilerArgs = [
    '-processor', processors.join(','),
    '-processorpath', configurations.annotationProcessors.asPath,
    "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
]

task integrationTest(type: Test) {
  systemProperty "webdriver.chrome.driver", "/Users/kengo/bin/chromedriver"
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

lombok {
  version = '1.16.18'
  sha256 = ''
}

wrapper {
  gradleVersion = '4.0'
}

build.dependsOn integrationTest
