ext.targetJavaVersion = JavaVersion.VERSION_1_8

buildscript {
  ext {
    springBootVersion = '2.1.1.RELEASE'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.6"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.16.0"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
  }
}

ext['selenium.version'] = '3.14.0'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/checker-framework.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/sonarqube.gradle"

version = '0.3.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url "https://repo.spring.io/milestone"
  }
}

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

dependencies {
  compile('org.apache.maven:maven-artifact:3.6.0')
  compile('org.springframework.boot:spring-boot-starter-webflux'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }
  runtimeOnly 'org.springframework.boot:spring-boot-starter-undertow'
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  compileOnly('com.google.code.findbugs:jsr305:3.0.2')
  compile('org.codehaus.janino:janino:3.0.11')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('com.codeborne:selenide:5.1.0')
  testCompile('io.projectreactor:reactor-test')

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
  systemProperty 'remote', 'http://localhost:4444/wd/hub'
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

wrapper {
  gradleVersion = '4.9'
}

build.dependsOn integrationTest
defaultTasks 'spotlessApply', 'build'
