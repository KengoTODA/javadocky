buildscript {
  ext {
    springBootVersion = '2.0.0.M2'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

version = '0.3.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url "https://repo.spring.io/milestone"
  }
  // for SPR-15761
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
}

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

dependencies {
  compile('org.apache.maven:maven-artifact:3.5.0')
  compile('org.springframework.boot:spring-boot-starter-webflux')
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  compileOnly "org.projectlombok:lombok:1.16.16"
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('com.codeborne:selenide:4.5.1')

  // for SPR-15761
  compile 'org.thymeleaf:thymeleaf-spring5:3.0.7-SNAPSHOT'

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test) {
  // for SPR-15707
  systemProperty "io.netty.buffer.bytebuf.checkAccessible", "false"

  systemProperty "webdriver.chrome.driver", "/Users/kengo/bin/chromedriver"
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

wrapper {
  gradleVersion = '4.0'
}

build.dependsOn integrationTest
