plugins {
  id "java"
  id 'jacoco'
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "org.springframework.boot" version "2.6.3"
  id "conventions"
}

repositories {
  mavenCentral()
}

apply from: "$rootDir/gradle/integration-test.gradle"

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib"
  implementation 'org.apache.maven:maven-artifact:3.8.4'
  implementation('org.springframework.boot:spring-boot-starter-webflux'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.codehaus.janino:janino'
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

def getBranch() {
  def process = 'git branch --show-current'.execute()
  process.waitFor()
  return process.text.trim()
}

def getHash() {
  def process = 'git rev-parse HEAD'.execute()
  process.waitFor()
  return process.text.trim()
}

bootJar {
  doFirst {
    manifest {
      attributes (
        'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
        'Created-By': "Gradle ${gradle.gradleVersion}",
        'Git-Branch': getBranch(),
        'Git-Hash': getHash()
      )
    }
  }
}

defaultTasks 'spotlessApply', 'build'
