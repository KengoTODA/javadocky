ext.targetJavaVersion = JavaVersion.VERSION_1_8

buildscript {
  ext {
    springBootVersion = '2.0.3.RELEASE'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "io.franzbecker:gradle-lombok:1.14"
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.14"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.13.0"
    classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.20.1"
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: "net.ltgt.errorprone"
apply plugin: 'com.palantir.docker-run'
apply from: "$rootDir/gradle/spotless.gradle"

version = '0.3.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url "https://repo.spring.io/milestone"
  }
}

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

configurations {
  checkerFrameworkAnnotatedJDK {
    description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
  }
}

ext {
  checkerFrameworkVersion = '2.5.3'
  jdkVersion = 'jdk8'
}

dependencies {
  compile('org.apache.maven:maven-artifact:3.5.4')
  compile('org.springframework.boot:spring-boot-starter-webflux')
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('com.codeborne:selenide:4.12.3')
  testCompile('io.projectreactor:reactor-test')

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime

  // for checker-framework
  checkerFrameworkAnnotatedJDK "org.checkerframework:${jdkVersion}:${checkerFrameworkVersion}"
  annotationProcessor "org.checkerframework:checker:${checkerFrameworkVersion}"
  compile "org.checkerframework:checker-qual:${checkerFrameworkVersion}"

  // for errorprone
  errorprone 'com.google.errorprone:error_prone_core:2.3.1'
  annotationProcessor 'jp.skypencil.errorprone.slf4j:errorprone-slf4j:0.1.2'
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    // '-Xep:Slf4jLoggerShouldBeNonStatic:OFF',
    '-Xep:ParameterName:OFF'
  ]
}

dockerRun {
  // docker-selenium image that contains both of Selenium Standalone Server with Google Chrome
  // https://github.com/SeleniumHQ/docker-selenium/tree/master/StandaloneChrome#how-to-use-this-image
  name 'javadocky_selenium'
  image 'selenium/standalone-chrome'
  ports '4444:4444'
  clean true
}

task integrationTest(type: Test) {
  dependsOn tasks.dockerRun
  finalizedBy tasks.dockerStop
  systemProperty 'remote', 'http://localhost:4444/wd/hub'
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

lombok {
  version = '1.18.0'
  sha256 = ''
}

wrapper {
  gradleVersion = '4.9'
}

build.dependsOn integrationTest
defaultTasks 'spotlessApply', 'build'
