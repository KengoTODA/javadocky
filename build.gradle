buildscript {
  ext {
    springBootVersion = '2.0.4.RELEASE'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.16"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.14.0"
    classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.20.1"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker-run'
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/checker-framework.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/sonarqube.gradle"
apply from: "$rootDir/gradle/jigsaw.gradle"

version = '0.3.0-SNAPSHOT'

repositories {
  mavenCentral()
  maven {
    url "https://repo.spring.io/milestone"
  }
}

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
  }
}

dependencies {
  implementation('org.apache.maven:maven-artifact:3.5.4')
  implementation('org.springframework.boot:spring-boot-starter-webflux')
  implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
  compileOnly('com.google.code.findbugs:jsr305:3.0.2')
  testImplementation('org.springframework.boot:spring-boot-starter-test')
  testImplementation('com.codeborne:selenide:4.12.3')
  testImplementation('io.projectreactor:reactor-test')

  integrationTestCompile sourceSets.main.output
  integrationTestCompile sourceSets.test.output
  integrationTestCompile configurations.testCompile
  integrationTestRuntime configurations.testRuntime
}

dockerRun {
  // docker-selenium image that contains both of Selenium Standalone Server with Google Chrome
  // https://github.com/SeleniumHQ/docker-selenium/tree/master/StandaloneChrome#how-to-use-this-image
  name 'javadocky_selenium'
  image 'selenium/standalone-chrome'
  ports '4444:4444'
  clean true
}

task integrationTest(type: Test) {
  dependsOn tasks.dockerRun
  finalizedBy tasks.dockerStop
  systemProperty 'remote', 'http://localhost:4444/wd/hub'
  testClassesDirs = files(sourceSets.integrationTest.java.outputDir)
  classpath = sourceSets.integrationTest.runtimeClasspath
}

wrapper {
  gradleVersion = '4.9'
}

build.dependsOn integrationTest
defaultTasks 'spotlessApply', 'build'
