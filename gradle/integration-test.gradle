// refs https://docs.gradle.org/current/userguide/java_testing.html

sourceSets {
  integrationTest {
    java {
      srcDir 'src/integration-test/java'
    }
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
  integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api'
  integrationTestImplementation 'io.github.bonigarcia:selenium-jupiter:3.3.0'
  integrationTestImplementation 'com.codeborne:selenide:5.2.8'
  integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.jupiter'
  }
  integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  integrationTestRuntimeOnly 'io.percy:percy-java-selenium:0.0.6'
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  useJUnitPlatform()
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  shouldRunAfter test
}

check.dependsOn integrationTest
